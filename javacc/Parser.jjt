options
{
    LOOKAHEAD = 1;
    MULTI = true;
}

PARSER_BEGIN(Parser)

public class Parser {

}

PARSER_END(Parser)

SKIP :
{
	" " | "\r" | "\t" | "\n"
}

/* COMMENTS */

MORE :
{
  "//" : IN_SINGLE_LINE_COMMENT
|
  <"/**" ~["/"]> { input_stream.backup(1); } : IN_FORMAL_COMMENT
|
  "/*" : IN_MULTI_LINE_COMMENT
}

<IN_SINGLE_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <SINGLE_LINE_COMMENT: "\n" | "\r" | "\r\n"> : DEFAULT
}

<IN_FORMAL_COMMENT>
SPECIAL_TOKEN :
{
  <FORMAL_COMMENT: "*/" > : DEFAULT
}

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <MULTI_LINE_COMMENT: "*/" > : DEFAULT
}

<IN_SINGLE_LINE_COMMENT,IN_FORMAL_COMMENT,IN_MULTI_LINE_COMMENT>
MORE :
{
  < ~[] >
}

TOKEN:
{
    <IMPORT: "import">
    | <STATIC: "static">
    | <DOT: ".">
    | <STAR: "*">
    | <SEMICOLON: ";">
    | <COMMA: ",">
    | <CLASS: "class">
    | <EXTENDS: "extends">
    | <LBRACE: "{">
    | <RBRACE: "}">
    | <PUBLIC: "public">
    | <LPAREN: "(">
    | <RPAREN: ")">
    | <RETURN: "return">
    | <VOID: "void">
    | <MAIN: "main">
    | <STRINGARR: "String[]">
    | <INT: "int">
    | <BOOLEAN: "boolean">
    | <IF: "if">
    | <ELSE: "else">
    | <WHILE: "while">
    | <ASSIGN: "=">
    | <LBRACKET: "[">
    | <RBRACKET: "]">
    | <SC_AND: "&&">
    | <LESS: "<">
    | <PLUS: "+">
    | <MINUS: "-">
    | <SLASH: "/">
    | <TRUE: "true">
    | <FALSE: "false">
    | <THIS: "this">
    | <NEW: "new">
    | <BANG: "!">
    | <LENGTH: "length">
    | <IDENTIFIER: ["a"-"z", "A"-"Z", "_", "$"](["a"-"z", "A"-"Z", "0"-"9", "_", "$"])* >
    | <NUMERIC: (["0"-"9"])+ >
}

SimpleNode Program() : {}
{
    ( ImportDeclaration() )* ClassDeclaration() <EOF> {return jjtThis;}
}


void ImportDeclaration() : {}
{
    <IMPORT> Name() <SEMICOLON>
}

/*
 * A lookahead of 2 is required below since "Name" can be followed
 * by a ".*" when used in the context of an "ImportDeclaration".
 */
void Name() : {Token t;}
{
    t = <IDENTIFIER> {jjtThis.value = t.image;} (LOOKAHEAD(2) <DOT> t = <IDENTIFIER> {jjtThis.value = t.image;} )*
}

void ClassDeclaration() : {Token t;}
{
    <CLASS>
    t=<IDENTIFIER> {jjtThis.value = t.image;}
    (<EXTENDS> t=<IDENTIFIER> {jjtThis.value = t.image;})?
    <LBRACE>
    (VarDeclaration() <SEMICOLON>)*
    (MethodDeclaration())*
    <RBRACE>
}

void VarDeclaration() : {}
{
    Type() Identifier()
}

void MethodDeclaration() : {Token t;}
{
    <PUBLIC>
    (
        Type()
        t=<IDENTIFIER> {jjtThis.value = t.image;}
        <LPAREN>
        (
            VarDeclaration()
            (
                <COMMA> VarDeclaration()
            )*
        )?
        <RPAREN>
        <LBRACE>
        MethodDeclarationBody()
        <RETURN> Expression() <SEMICOLON>
        <RBRACE>
    |
         <STATIC>
         <VOID>
         <MAIN>
         <LPAREN>
         t=<STRINGARR> {jjtThis.value = t.image;}
         Identifier()
         <RPAREN>
         <LBRACE>
         MethodDeclarationBody()
         <RBRACE>
    )
}

void Identifier() : { Token t; }
{
    t = <IDENTIFIER> {jjtThis.value = t.image;}
}

void MethodDeclarationBody() : {}
{
    (BlockStatement())*
}

void BlockStatement() : {}
{
    LOOKAHEAD(2)
    LocalVariableDeclaration()
    |
    Statement()
}

void LocalVariableDeclaration() : {}
{
    Type() VariableDeclarator() (<COMMA> VariableDeclarator())* <SEMICOLON>
}

void Type() : {Token t;}
{
    t = <IDENTIFIER> {jjtThis.value = t.image;}
    |
    PrimitiveType() // "int", "[", "]", "boolean", "int"
}

void PrimitiveType() : {Token t; Boolean b = false;}
{
    t = <BOOLEAN> {jjtThis.value = t.image;}
    |
    (
        t = <INT> (<LBRACKET> <RBRACKET> {b = true;})? {
            if(b)
                jjtThis.value = t.image + "[]"; // int[]
            else
                jjtThis.value = t.image; // int
        }
    )
}

void VariableDeclarator() : {}
{
    VariableDeclaratorId() [<ASSIGN> VariableInitializer()]
}

void VariableDeclaratorId() : {Token t;}
{
    t=<IDENTIFIER> {jjtThis.value = t.image;} (<LBRACKET> <RBRACKET>)*
}

void VariableInitializer() : {}
{
    ArrayInitializer()
    |
    Expression()
}

void ArrayInitializer() : {}
{
    <LBRACE> [VariableInitializer() (LOOKAHEAD(2) <COMMA> VariableInitializer())*][<COMMA>] <RBRACE>
}

void Expression() : {}
{
    ConditionalAndExpression()
}

void ConditionalAndExpression() : {}
{
    RelationalExpression() (<SC_AND> RelationalExpression())*
}

void RelationalExpression() : {}
{
    AdditiveExpression() (<LESS> AdditiveExpression())*
}

void AdditiveExpression() : {}
{
    MultiplicativeExpression() ((<PLUS> | <MINUS>) MultiplicativeExpression())*
}

void MultiplicativeExpression() : {}
{
    UnaryExpression() ((<STAR> | <SLASH>) UnaryExpression())*
}

void UnaryExpression() : {}
{
    UnaryExpressionNotPlusMinus()
}

void UnaryExpressionNotPlusMinus() : {}
{
    <BANG> UnaryExpression()
    |
    PrimaryExpression()
}

void PrimaryExpression() : {}
{
    PrimaryPrefix() ( LOOKAHEAD(2) PrimarySuffix() )*
}

void PrimaryPrefix() : {}
{
    Literal()
    |
    <THIS>
    |
    <LPAREN> Expression() <RPAREN>
    |
    AllocationExpression()
}

void PrimarySuffix() : {Token t;}
{
    LOOKAHEAD(2)
    <DOT> t=<IDENTIFIER> {jjtThis.value = t.image;}
    |
    <LBRACKET> Expression() <RBRACKET>
    |
    <DOT> <LENGTH>
    |
    Arguments()
}

void Literal() : {Token t;}
{
    t=<NUMERIC> {jjtThis.value = t.image;}
    |
    t=<IDENTIFIER> {jjtThis.value = t.image;}
    |
    BooleanLiteral()
}

void BooleanLiteral() : {Token t;}
{
    t=<TRUE> {jjtThis.value = t.image;}
    |
    t=<FALSE> {jjtThis.value = t.image;}
}

void Arguments() : {}
{
    <LPAREN> [ArgumentList()] <RPAREN>
}

void ArgumentList() : {}
{
    Expression() (<COMMA> Expression())*
}

void AllocationExpression() : {Token t;}
{
    <NEW>
    (
        t=<INT> InsideArray() {jjtThis.value = t.image + "[]";}
        |
        t=<IDENTIFIER> <LPAREN> <RPAREN> {jjtThis.value = t.image;}
    )
}

void InsideArray() : {}
{
    <LBRACKET> Expression() <RBRACKET>
}

void Statement() : {}
{
    IfStatement()
    |
    WhileStatement()
    |
    ( LOOKAHEAD(2)
        IdentifierStatement()
        |
        Expression() <SEMICOLON>
    )
    |
    <LBRACE> (Statement())* <RBRACE>
}

void IfStatement() : {}
{
    <IF> <LPAREN> Expression() <RPAREN> Statement()
    <ELSE> Statement()
}

void WhileStatement() : {}
{
    <WHILE> <LPAREN> Expression() <RPAREN> Statement()
}

void IdentifierStatement() : {} // Identifier, "=", Expression, ";" | Identifier, "[", Expression, "]", "=", Expression, ";" | Expression, ";"
{
    Identifier() AfterIdentifier() <SEMICOLON>
}

void AfterIdentifier() : {}
{
    <ASSIGN> Expression()
    |
    InsideArray() <ASSIGN> Expression()
}

/*

JAVACODE
void recover_while_error() {
    ParseException e = generateParseException();
    System.out.println(e.toString());
    Token t;

    //skip tokens until first parenthesis is reacherd
    do {
        t = getNextToken();
    } while (t.kind != ParenthesisEnd);

    //skip extra parenthesis
    while (getToken(1).kind == ParenthesisEnd) {
        t = getNextToken();
    }
}

*/


/*
JAVACODE void error_skipto(int kind) #SyntaticError
{
  Json.syntaticError = true;
  ParseException e = generateParseException()
; // generate the exception object.
System.out.println("Error found at column "+ e.currentToken.beginColumn+ " line "+ e.currentToken.beginLine);
Token t;
do
{
  t = getNextToken();
  if (t.kind == CBRA) return;
}

while (t.kind != kind);
}*/
