options
{
    LOOKAHEAD = 1;
    MULTI = true;
}

PARSER_BEGIN(Parser)

import pt.up.fe.comp.jmm.report.Report;
import pt.up.fe.comp.jmm.report.ReportType;
import pt.up.fe.comp.jmm.report.Stage;
import java.util.ArrayList;

public class Parser {

    int errors = 0;
    ArrayList<Report> syntacticErrors =  new ArrayList<Report>();

    ArrayList<Report> getSyntacticErrors() {
        return syntacticErrors;
    };

    void errorFunction(ParseException e) {
        Report error = new Report(ReportType.ERROR,Stage.SYNTATIC,e.currentToken.beginLine,"Encountered \"" + e.currentToken.image+ "\", column " + e.currentToken.beginColumn + ".");
        this.syntacticErrors.add(error);

        this.errors ++;
        //System.out.prisntln("ParseException Error: Encountered \""+ e.currentToken.image+ "\" at line "+ e.currentToken.beginLine+", column " + e.currentToken.beginColumn + ".");

        Token t = getToken(0);
        // Advances while there are no 2 ")" in a row or if the following character is not a "{"
        while (true) {
            if (t.kind == RPAREN && getToken(1).kind != RPAREN) {
                break;
            }
            else if (getToken(1).kind == LBRACE){
                break;
            }
            else if (getToken(1).kind == EOF)
            {
                break;
            }

            t = getNextToken();
        }

        if (this.errors>= 10) {
            for(Report r : this.syntacticErrors) {
               System.out.println(r.toString());
            }
            throw new RuntimeException("Ten errors were found. Program terminated.");
        }
    };
}

PARSER_END(Parser)

SKIP :
{
	" " | "\r" | "\t" | "\n"
}

/* COMMENTS */

MORE :
{
  "//" : IN_SINGLE_LINE_COMMENT
|
  <"/**" ~["/"]> { input_stream.backup(1); } : IN_FORMAL_COMMENT
|
  "/*" : IN_MULTI_LINE_COMMENT
}

<IN_SINGLE_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <SINGLE_LINE_COMMENT: "\n" | "\r" | "\r\n"> : DEFAULT
}

<IN_FORMAL_COMMENT>
SPECIAL_TOKEN :
{
  <FORMAL_COMMENT: "*/" > : DEFAULT
}

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <MULTI_LINE_COMMENT: "*/" > : DEFAULT
}

<IN_SINGLE_LINE_COMMENT,IN_FORMAL_COMMENT,IN_MULTI_LINE_COMMENT>
MORE :
{
  < ~[] >
}

TOKEN:
{
    <IMPORT: "import">
    | <STATIC: "static">
    | <DOT: ".">
    | <STAR: "*">
    | <SEMICOLON: ";">
    | <COMMA: ",">
    | <CLASS: "class">
    | <EXTENDS: "extends">
    | <LBRACE: "{">
    | <RBRACE: "}">
    | <PUBLIC: "public">
    | <LPAREN: "(">
    | <RPAREN: ")">
    | <RETURN: "return">
    | <VOID: "void">
    | <MAIN: "main">
    | <STRINGARR: "String[]">
    | <INT: "int">
    | <BOOLEAN: "boolean">
    | <IF: "if">
    | <ELSE: "else">
    | <WHILE: "while">
    | <ASSIGN: "=">
    | <LBRACKET: "[">
    | <RBRACKET: "]">
    | <SC_AND: "&&">
    | <LESS: "<">
    | <PLUS: "+">
    | <MINUS: "-">
    | <SLASH: "/">
    | <TRUE: "true">
    | <FALSE: "false">
    | <THIS: "this">
    | <NEW: "new">
    | <BANG: "!">
    | <LENGTH: "length">
    | <IDENTIFIER: ["a"-"z", "A"-"Z", "_", "$"](["a"-"z", "A"-"Z", "0"-"9", "_", "$"])* >
    | <NUMERIC: (["0"-"9"])+ >
}

SimpleNode Program() : {}
{
    ( ImportDeclaration() )* ClassDeclaration() <EOF> {

    if (errors>= 1) {
        for(Report r : syntacticErrors) {
          System.out.println(r.toString());
        }
    }
    return jjtThis;
    }
}


void ImportDeclaration() : {}
{
    <IMPORT> Name() <SEMICOLON>
}

/*
 * A lookahead of 2 is required below since "Name" can be followed
 * by a ".*" when used in the context of an "ImportDeclaration".
 */
void Name() #void : {}
{
    Identifier() (LOOKAHEAD(2) <DOT> Identifier() )*
}

void ClassDeclaration() : {}
{
    <CLASS>
    Identifier()
    (<EXTENDS> Identifier() #Extends(1))?
    <LBRACE>
    (VarDeclaration() <SEMICOLON>)*
    (MethodDeclaration())*
    <RBRACE>
}

void VarDeclaration() : {}
{
    LOOKAHEAD(2)
    Type() Identifier()
    |
    Expression()
}

void MethodDeclaration() #void : {}
{
    LOOKAHEAD(2)
    Method()
    |
    Main()
}

void Identifier() #Identifier : { Token t; }
{
    t = <IDENTIFIER> {jjtThis.value = t.image; jjtThis.put("name", t.image);}
}

void Method() : {}
{
    <PUBLIC>
    Type()
    Identifier()
    <LPAREN>
    (
        Args()
    )?
    <RPAREN>
    <LBRACE>
    MethodDeclarationBody()
    <RETURN> #Return Expression() <SEMICOLON>
    <RBRACE>
}

void Args() : {}
{
   VarDeclaration()
   (
       <COMMA> VarDeclaration()
   )*
}

void Main() : {}
{
    <PUBLIC>
    <STATIC>
    <VOID>
    <MAIN>
    <LPAREN>
    <STRINGARR>
    Identifier()
    <RPAREN>
    <LBRACE>
    MethodDeclarationBody()
    <RBRACE>
}

void MethodDeclarationBody() : {}
{
    (BlockStatement())*
}

void BlockStatement() #void: {}
{
    LOOKAHEAD(2)
    LocalVariableDeclaration()
    |
    Statement()
}

void LocalVariableDeclaration() : {}
{
    Type() VariableDeclarator() (<COMMA> VariableDeclarator())* <SEMICOLON>
}

void Type() #void : {}
{
    Identifier()
    |
    <BOOLEAN> #Boolean
    |
    LOOKAHEAD(2)
    <INT> <LBRACKET> <RBRACKET> #ArrayInt
    |
    <INT> #Integer

}

void VariableDeclarator() #void : {}
{
    VariableDeclaratorId() [<ASSIGN> VariableInitializer()]
}

void VariableDeclaratorId() #void : {Token t;}
{
    Identifier() (<LBRACKET> <RBRACKET>)*
}

void VariableInitializer() : {}
{
    ArrayInitializer()
    |
    Expression()
}

void ArrayInitializer() : {}
{
    <LBRACE> [VariableInitializer() (LOOKAHEAD(2) <COMMA> VariableInitializer())*][<COMMA>] <RBRACE>
}

void Expression() #void : {}
{
    ConditionalAndExpression()
}

void ConditionalAndExpression() #void : {}
{
    RelationalExpression() (<SC_AND> RelationalExpression() #And(2))*
}

void RelationalExpression() #void : {}
{
    AdditiveExpression() (<LESS> AdditiveExpression() #Less(2))*
}

void AdditiveExpression() #void : {Token t;}
{
    SubtrativeExpression() (t=<PLUS> SubtrativeExpression() {jjtThis.value = t.image; jjtThis.put("operation", t.image);} #AdditiveExpression(2))*
}

void SubtrativeExpression() #void : {Token t;}
{
    MultiplicativeExpression() (t=<MINUS> MultiplicativeExpression() {jjtThis.value = t.image; jjtThis.put("operation", t.image);} #SubtrativeExpression(2))*
}

void MultiplicativeExpression() #void : {Token t;}
{
    DivisionExpression() (t=<STAR> DivisionExpression() {jjtThis.value = t.image; jjtThis.put("operation", t.image);} #MultiplicativeExpression(2))*
}

void DivisionExpression() #void : {Token t;}
{
    UnaryExpressionNotPlusMinus() (t=<SLASH> UnaryExpressionNotPlusMinus() {jjtThis.value = t.image; jjtThis.put("operation", t.image);} #DivisionExpression(2))*
}

void UnaryExpressionNotPlusMinus() #void : {}
{
    <BANG> UnaryExpressionNotPlusMinus() #Not
    |
    PrimaryExpression()
}

void PrimaryExpression() #void : {}
{
    PrimaryPrefix() (PrimarySuffix())?
}

void PrimaryPrefix() #void : {}
{
    Literal()
    |
    <THIS> #This
    |
    <LPAREN> Expression() <RPAREN>
    |
    AllocationExpression() #New
}

void PrimarySuffix() #void : {}
{
    InsideArray()
    |
    DotExpression()
}

void DotExpression() #void : {}
{
    <DOT>
    (
        (<LENGTH> #Length) #Dot(2)
        |
        (Identifier() <LPAREN> (Args())? <RPAREN>) #Dot(2)
    )
}

void Literal() #void : {Token t;}
{
    t=<NUMERIC> {jjtThis.value = Integer.valueOf(t.image); jjtThis.put("value", t.image);} #Integer
    |
    Identifier()
    |
    BooleanLiteral()
}

void BooleanLiteral() #Boolean : {Token t;}
{
    t=<TRUE> {jjtThis.value = Boolean.valueOf(t.image); jjtThis.put("value", t.image);}
    |
    t=<FALSE> {jjtThis.value = Boolean.valueOf(t.image); jjtThis.put("value", t.image);}
}

void Arguments() #void : {}
{
    <LPAREN> [ArgumentList()] <RPAREN>
}

void ArgumentList() #void : {}
{
    Expression() (<COMMA> Expression())*
}

void AllocationExpression() #void : {Token t;}
{
    <NEW>
    (
        <INT> InsideArray() #Array(1)
        |
        Identifier() #Class <LPAREN> <RPAREN>
    )
}

void InsideArray() #void : {}
{
    <LBRACKET> Expression() <RBRACKET>
}

void Statement() : {}
{
    IfStatement()
    |
    WhileStatement()
    |
    ( LOOKAHEAD(2)
        IdentifierStatement()
        |
        Expression() <SEMICOLON>
    )
    |
    <LBRACE> (Statement())* <RBRACE>
}

void IfStatement() #void : {Token t, s;}
{
    t=<IF> {jjtThis.value = t.image;} #If <LPAREN> Expression() <RPAREN> Statement()
    s=<ELSE> {jjtThis.value = s.image;} #Else Statement()
}

void WhileStatement() #While : {Token t;}
{
   t=<WHILE> {jjtThis.value = t.image;} WhileCondition() Statement()
}

void WhileCondition() #void : {}
{
  try {
    <LPAREN> Expression() <RPAREN>}
    catch (ParseException e)
    {
        errorFunction(e);
    }
}

void IdentifierStatement() #void : {} // Identifier, "=", Expression, ";" | Identifier, "[", Expression, "]", "=", Expression, ";" | Expression, ";"
{
    Identifier() AfterIdentifier() <SEMICOLON>
}

void AfterIdentifier() #void : {}
{
    <ASSIGN> #Assign Expression()
    |
    InsideArray() <ASSIGN> #Assign Expression()
}
